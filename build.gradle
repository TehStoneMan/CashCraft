// For those who want the bleeding edge
buildscript
{
    repositories
    {
        jcenter()
        maven
        {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

// Define properties file
ext.configFile = file "gradle.properties"

configFile.withReader
{
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

archivesBaseName = "CashCraft"
group = "io.github.tehstoneman.cashcraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
version = "${config.minecraft_version}-${config.mod_major_version}.${config.api_major_version}.${config.minor_version}.${config.patch_version}"

// java version
compileJava
{
	sourceCompatibility = targetCompatibility = '1.8'
	options.compilerArgs << '-Xlint:unchecked'
}

minecraft
{
    version = "${config.minecraft_version}-${config.forge_version}"
    //mc_version = "${config.minecraft_version}"

	if( file('../run').exists() )
	{
		runDir = "../run/assets"
	}
	else
	{
		runDir = "assets"
	}

    mappings = "${config.mappings_version}"

    replaceIn "src/main/java/io/github/tehstoneman/cashcraft/ModInfo.java"
    replace "@minecraft_version@", config.minecraft_version
    replace "@mod_major_version@", config.mod_major_version
    replace "@api_major_version@", config.api_major_version
    replace "@minor_version@", config.minor_version
    replace "@patch_version@", config.patch_version
    replace "@forge_version@", config.forge_version
}

sourceSets
{
    main
    {
        java
        {
        	srcDirs 'src/main/java'
        }
        resources
        {
        	srcDirs 'src/main/resources'
        }
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}
